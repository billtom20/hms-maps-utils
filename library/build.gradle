/**
 * Copyright 2020 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

def innerGroupId = "com.github.billtom20"
def innerArtifactId = "hms-maps-utils"
def releaseVersion = "2.4.0"
def releaseTime = new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
group = "${innerGroupId}"
version = "${releaseVersion}"

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        consumerProguardFiles 'consumer-rules.pro'
        // This enables us to tell when we're running unit tests on Travis (#573)
        buildConfigField("String", "TRAVIS", "\"" + System.getenv('TRAVIS') + "\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            android.libraryVariants.all { variant ->
                if (!System.getenv("JITPACK")) {
                    variant.outputs.all {
                        outputFileName = "${innerArtifactId}-${releaseVersion}-${releaseTime}.aar"
                    }
                }
            }
        }
    }
    resourcePrefix 'amu_'
    // This enables long timeouts required on slow environments, e.g. Travis
    adbOptions {
        timeOutInMs 10 * 60 * 1000  // 10 minutes
        installOptions "-d", "-t"
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

dependencies {
    implementation "com.huawei.hms:maps:$map_version"
    implementation 'androidx.appcompat:appcompat:1.3.1'
    lintPublish project(':lint-checks')
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.7.3'
    testImplementation 'net.sf.kxml:kxml2:2.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

task instrumentTest(dependsOn: connectedCheck)

if (System.getenv("JITPACK")) {
    apply plugin: 'maven-publish'
    afterEvaluate {
        publishing {
            publications {
                // Creates a Maven publication called "release".
                release(MavenPublication) {
                    from components.release
                    groupId = "${innerGroupId}"
                    artifactId = "${innerArtifactId}"
                    version = "${releaseVersion}"
                }
            }
        }
    }
}

